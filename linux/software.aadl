package Software
public
  with Data_Model;

  ----------
  -- Data --
  ----------

-- on va chercher dans base_types direectement pas dans data:model::data-representation
-- DOUBLE A VERIFIER

  data Double_Type extends Base_Types::Float_64
  end Double_Type;


  data Array_Type
  properties
    Data_Model::Data_Representation => Array;
    Data_Model::Base_Type => (classifier (Double_Type));
    Data_Model::Dimension => (4);
  end Array_Type;


  -----------------
  -- Subprograms --
  -----------------

  subprogram Send_Pos
  features
    Data_Source : out parameter Array_type;
  properties
    Source_Language => (C);
    Source_Name     => "compute_send_position";
    Source_Text     => ("ciel_etoile.c");
  end Send_Pos;

  subprogram Receive_Send
  features
    Data_Sink : in parameter Array_type;
  properties
    Source_Language => (C);
    Source_Name     => "receive_compute_send";
    Source_Text     => ("ciel_etoile.c");
  end Receive_Send;

  -------------
  -- Threads --
  -------------

  thread S
  features
    Data_Source : out event data port Array_type;
  end S;

  thread implementation S.Impl
  calls
    Mycalls: {
    S_Spg : subprogram Send_Pos;
    };
  connections
    parameter S_Spg.Data_Source -> Data_Source;
  properties
    Dispatch_Protocol => Periodic;
    Period            => 5 sec;
    Priority => 200;
  end S.Impl;

  thread RS
  features
    Data_Sink : in event data port Array_type;
  end RS;

  thread implementation RS.Impl
  calls
    Mycalls: {
    RS_Spg : subprogram Receive_Send;
    };
  connections
    parameter Data_Sink -> RS_Spg.Data_Sink;
  properties
    Dispatch_Protocol => Sporadic;
    Period            => 100 ms;
    Priority => 100;
  end RS.Impl;

end Software;
